<?xml version="1.0" encoding="ISO-8859-1"?>
<xsl:stylesheet version="1.0" xmlns:xsl="http://www.w3.org/1999/XSL/Transform">
  <xsl:output encoding="ISO-8859-1" indent="yes" method="html"/>
  <xsl:param name="numFicha">1</xsl:param>

  <xsl:template match="/">
    <html>
      <head>
        <link rel="stylesheet" href="https://www.w3schools.com/w3css/4/w3.css"/>
        <link href="https://fonts.googleapis.com/icon?family=Material+Icons"
          rel="stylesheet"/>
        <title>Guião de aula pática</title>
        <meta charset="UTF-8"/>
        <meta name="viewport" content="width=device-width, initial-scale=1"/>
      </head>
      <body>
        <div class="w3-container">
          <header class="w3-container w3-center w3-blue">
            <h1>Ficha id: 
              <xsl:choose>
                <xsl:when test="AulaPrática/@id">
                  <xsl:value-of select="AulaPrática/@id"/>
                </xsl:when>
                <xsl:otherwise> [no id] </xsl:otherwise>
              </xsl:choose>
            </h1>
            <h3><xsl:value-of select="AulaPrática/meta/disciplina"/></h3>
          </header>
          <p class="w3-left-align">
            <xsl:apply-templates select="AulaPrática/meta/objectivos"/>
          </p>
          <xsl:call-template name="driver"/>
          <xsl:apply-templates select="AulaPrática/corpo/exercício"/>
          
          <footer class="w3-container w3-blue">
            <p>Generated by xfichas app by jcr::2018-02-07</p>
          </footer>
        </div>
      </body>
    </html>

  </xsl:template>

  <xsl:template name="driver">
    <div class="w3-row-padding">
      <div class="w3-col s6">
        <div class="w3-card-4">
          
          <header class="w3-container w3-blue">
            <h3>Exercícios</h3>
          </header>
          
          <div class="w3-container">
            <ul class="w3-ul">
              <xsl:for-each select="AulaPrática/corpo/exercício">
                <a name="ind-ex"/>
                <li>
                  <a href="#e{count(preceding-sibling::exercício)+1}">
                    <xsl:value-of select="título"/>
                  </a>
                </li>
              </xsl:for-each>
            </ul>
          </div>
        </div>
      </div>
      <div class="w3-col s6">
        <div class="w3-card-4">
          
          <header class="w3-container w3-blue">
            <h3>Recursos</h3>
          </header>
          <div class="w3-container">
            <ul class="w3-ul">
              <xsl:for-each select="AulaPrática/meta/recursos/documento">
                <a name="ind-ex"/>
                <li>
                  &#8226;
                  <a href="{@url}">
                    <xsl:value-of select="."/>
                  </a>
                </li>
              </xsl:for-each>
            </ul>
          </div>
        </div>
      </div>
    </div>
  </xsl:template>

  <xsl:template match="introdução">
    <xsl:apply-templates/>
  </xsl:template>

  <xsl:template match="datas">
    <center>
      <xsl:for-each select="data">
        <xsl:value-of select="."/> (<xsl:value-of select="@tipo"/>) <br/>
      </xsl:for-each>
    </center>
  </xsl:template>

  <xsl:template match="exercício">
    <div class="w3-card-4 w3-padding">
      <a name="e{count(preceding-sibling::exercício)+1}"/>
      <header class="w3-container w3-blue">
        <h5>Exercício Nº <xsl:value-of select="count(preceding-sibling::exercício)+1"/>:
          <xsl:value-of select="título"/></h5>
      </header>
      
      <div class="w3-container">
        <xsl:apply-templates select="enunciado"/>
      </div>
      
      <footer class="w3-container w3-blue">
        <a href="http://localhost:3000/fichas"><i class="material-icons">home</i></a>
        <a href="#ind-ex"><i class="material-icons">list</i></a>
      </footer>
      
    </div>
    
    <xsl:if test="solucao">
      <div class="panel panel-default">
        <div class="panel-heading">
          <a name="s{count(preceding-sibling::exercício)+1}"/>
          <h3 class="panel-title">Solução do exercício Nº <xsl:value-of select="count(preceding-sibling::exercício)+1"/>:
            <xsl:value-of select="título"/>
          </h3>
        </div>
        <div class="panel-body">
          <xsl:apply-templates select="solucao"/>
        </div>
        <div class="panel-footer"> [<a href="#ind-ex">Voltar ao Índice de exercícios</a>] </div>
      </div>
    </xsl:if>
    
  </xsl:template>

  <xsl:template match="enunciado">
    <xsl:apply-templates/>
  </xsl:template>

  <xsl:template match="imagem">
    <div class="img">
      <center>
        <xsl:choose>
          <xsl:when test="grafico/@width">
            <img src="{grafico/@path}" width="{grafico/@width}"/>
          </xsl:when>
          <xsl:otherwise>
            <img src="{grafico/@path}"/>
          </xsl:otherwise>
        </xsl:choose>
        <p>
          <b>Fig.<xsl:value-of select="count(preceding::imagem)+1"/>: <xsl:value-of select="legenda"/></b>
        </p>
      </center>
    </div>
  </xsl:template>

  <xsl:template match="para">
    <p>
      <xsl:apply-templates/>
    </p>
  </xsl:template>

  <xsl:template match="lista">
    <ul class="w3-ul">
      <xsl:apply-templates/>
    </ul>
  </xsl:template>

  <xsl:template match="listan">
    <ol>
      <xsl:apply-templates/>
    </ol>
  </xsl:template>

  <xsl:template match="listad">
    <dl>
      <xsl:apply-templates/>
    </dl>
  </xsl:template>

  <xsl:template match="listan/item|lista/item">
    <li>
      &#8226;
      <xsl:apply-templates/>
    </li>
  </xsl:template>

  <xsl:template match="termo">
    <dt>
      <xsl:apply-templates/>
    </dt>
  </xsl:template>

  <xsl:template match="descricao">
    <dd>
      <xsl:apply-templates/>
    </dd>
  </xsl:template>

  <xsl:template match="item">
    <xsl:apply-templates/>
  </xsl:template>

  <xsl:template match="realce[@tipo='emph']">
    <i>
      <xsl:apply-templates/>
    </i>
  </xsl:template>

  <xsl:template match="realce">
    <b>
      <xsl:apply-templates/>
    </b>
  </xsl:template>

  <xsl:template match="codigo">
    <pre class="codigo">
      <xsl:apply-templates/>
    </pre>
  </xsl:template>
  <xsl:template match="alíneas">
    <ol class="a">
      <xsl:apply-templates/>
    </ol>
  </xsl:template>
  <xsl:template match="alínea">
    <li>
      <xsl:apply-templates/>
    </li>
  </xsl:template>
  <xsl:template match="kw">
    <b>
      <xsl:apply-templates/>
    </b>
  </xsl:template>
  <xsl:template match="xref">
    <a href="{@url}">
      <xsl:value-of select="."/>
    </a>
  </xsl:template>

  <xsl:template match="tabela">
    <table border="1">
      <xsl:apply-templates/>
    </table>
  </xsl:template>
  <xsl:template match="linha">
    <tr>
      <xsl:apply-templates/>
    </tr>
  </xsl:template>
  <xsl:template match="celula">
    <td>
      <xsl:apply-templates/>
    </td>
  </xsl:template>
</xsl:stylesheet>
